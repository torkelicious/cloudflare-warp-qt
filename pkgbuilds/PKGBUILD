pkgname=cloudflare-warp-qt-git
pkgver=latest
pkgrel=1
pkgdesc="Qt6 C++ tray application for Cloudflare WARP via warp-cli"
arch=("x86_64")
url="https://github.com/torkelicious/cloudflare-warp-qt"
license=("GPL-3.0-or-later")
depends=("qt6-base" "cloudflare-warp-bin")
makedepends=("git" "cmake" "qt6-tools")
#provides=("cloudflare-warp-qt")
#conflicts=("cloudflare-warp-qt")

source=("${pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${pkgname}"
  cmake -S . -B build \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build --parallel
}

package() {
  cd "${pkgname}"
  DESTDIR="${pkgdir}" cmake --install build
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
